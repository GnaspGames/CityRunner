--###########--
--   GAME    --
--###########--

-- The main game code

# TRIGGER START
!loop TriggerStart
	{"type":"chain", "auto":true, "conditional":false}
	{"executeAs":"@a[score_trigger_start_min=1]"}
		!start_event StartCountdown
		-- Announce who started game
		/tellraw @a ["",{"selector":"@p","color":"yellow","bold":"true"},{"text":" started a new race...","color":"yellow","bold":"true"}]
		-- Stop players using trigger_start
		!stop_loop TriggerStart
		/scoreboard players reset @a trigger_start
	{"executeAs":""}

# START COUNTDOWN
-- Start a countdown that leads to the game start
!event StartCountdown
	{"type":"chain", "auto":true, "conditional":false}
	-- Set correct title times
	/title @a times 0 40 20 
	-- Start countdown scoreboard
	/scoreboard players set @e[type=ArmorStand,name=SYSTEM] countdown 100
	-- Turn on countdown loop
	!start_loop Countdown

# COUNTDOWN
!loop Countdown
	{"type":"chain", "auto":true, "conditional":false}
	{"executeAs":"@e[name=SYSTEM,score_countdown=100,score_countdown_min=100]"}
		/title @a reset
	{"executeAs":"@e[name=SYSTEM,score_countdown=60,score_countdown_min=60]"}
		/title @a title {"text":"3","color":"red","bold":"true"}
		
	{"executeAs":"@e[name=SYSTEM,score_countdown=40,score_countdown_min=40]"}
		/title @a title {"text":"2","color":"gold","bold":"true"}
		
	{"executeAs":"@e[name=SYSTEM,score_countdown=20,score_countdown_min=20]"}
		/title @a title {"text":"1","color":"yellow","bold":"true"}
		
	{"executeAs":"@e[name=SYSTEM,score_countdown=0,score_countdown_min=0]"}
		/title @a title {"text":"Go!","color":"green","bold":"true"}
		!stop_loop Countdown
			{"conditional":true}
				!start_event StartGame
	{"executeAs":"", "conditional":false}
	/scoreboard players remove @e[type=ArmorStand,name=SYSTEM] countdown 1
	
# START ITEMS
{"type":"impulse", "auto":false, "conditional":false}
{"markerTag":"randomStartItem"}
	/give @a minecraft:ender_pearl 3
	/give @a minecraft:ender_pearl 5
	/give @a potion 2 0 { CustomPotionEffects: [{Id: 8, Amplifier: 20, Duration: 600}],display: {Name: "Jump Boost",Lore:["Jump higher for 30 seconds after drinking"]}}
	/give @a potion 2 0 { CustomPotionEffects: [{Id: 1, Amplifier: 10, Duration: 600}],display: {Name: "Speed Boost",Lore:["Run faster for 30 seconds after drinking"]}}
	/give @a potion 3 0 { CustomPotionEffects: [{Id: 25, Amplifier: 50, Duration: 10}],display: {Name: "Levitation",Lore:["Shoot up into the air when you drink this"]}}
	/give @a minecraft:iron_sword 1 0 {display:{Name:"Blade Runner",Lore:["Knock back the competition" ]},ench: [{id:19,lvl:5}]}
	/give @a minecraft:elytra 1 401 {display:{Name:"Glider",Lore:["Just 30 seconds to glide to the finish" ]}}
{"markerTag":""}

# START GAME
!event StartGame
	{"type":"chain", "auto":true, "conditional":false}
	-- Choose a new Start Point
	/scoreboard players tag @r[type=ArmorStand,name=cr_start_point] add ActiveStartPoint
	-- Set up all viable spawn points around start point
	/execute @e[tag=ActiveStartPoint] ~ ~ ~ scoreboard players tag @e[name=cr_start_spawn,r=10] add ActiveSpawnPoint
	-- Send players to a random spawn point.
	/execute @a ~ ~ ~ tp @p @r[type=ArmorStand,tag=ActiveSpawnPoint]
	/execute @a ~ ~ ~ spawnpoint ~ ~ ~	
	-- Clear all inventories
	/clear @a	
	-- Reset start point scores
	/scoreboard players tag @e[tag=ActiveStartPoint] remove ActiveStartPoint
	/scoreboard players tag @e[tag=ActiveSpawnPoint] remove ActiveSpawnPoint
	-- Give playets starting items
	/execute @r[type=ArmorStand,tag=randomStartItem] ~ ~ ~ blockdata ~ ~ ~ {auto:1b}
	/execute @e[type=ArmorStand,tag=randomStartItem] ~ ~ ~ blockdata ~ ~ ~ {auto:0b}
	-- Choose a new Finish Point
	/scoreboard players tag @r[type=ArmorStand,name=cr_finish_point] add ActiveFinishPoint
	{"executeAs":"@e[tag=ActiveFinishPoint]"}
		-- Set the beacon block to turn it on
		/setblock ~ ~ ~ minecraft:beacon
		-- Announce the area name
		/tellraw @a [{"text":"The finishing beacon is near: ","color":"green","bold":"true"},{"selector":"@e[tag=AreaName,c=1]"}]
	{"executeAs":""}
	-- Turn on loop for win conditions
	!start_loop DetectWin
	-- Put all players into adventure mode
	/gamemode adventure @a
	-- Reset DEATHS for all players
	/scoreboard players reset @a deaths
	/scoreboard players set @a deaths_dummy 0
	-- Start detecting deaths
	!start_loop DetectDeaths
	-- Start powerups
	!start_event StartPowerups
	-- Give everyone full health
	/effect @a minecraft:regeneration 1 20
	-- Display "Distance" scoreboard and start loop
	/scoreboard objectives setdisplay sidebar distance
	-- Set time for racing in dark
	/time set 13000
	!start_loop GameRunning
	-- TEMPORARY: Turn off builing tools
	!stop_loop ShowStartPoints
	!stop_loop ShowFinishPoints

# GAME RUNNING
!loop GameRunning
	{"type":"chain", "conditional":false}
	/scoreboard players tag @a remove HasSpeed
	/scoreboard players tag @a add HasSpeed {ActiveEffects:[{Id:1b}]}
	/effect @a[tag=!HasSpeed] minecraft:speed 1 3 true
	{"executeAs":"@e[tag=ActiveFinishPoint]"}
		/scoreboard players set @a[m=2,rm=1,r=2] distance -1
		/scoreboard players set @a[m=2,rm=2,r=3] distance -2
		/scoreboard players set @a[m=2,rm=3,r=4] distance -3
		/scoreboard players set @a[m=2,rm=4,r=5] distance -4
		/scoreboard players set @a[m=2,rm=5,r=10] distance -5
		/scoreboard players set @a[m=2,rm=10,r=15] distance -10
		/scoreboard players set @a[m=2,rm=15,r=20] distance -15
		/scoreboard players set @a[m=2,rm=20,r=25] distance -20
		/scoreboard players set @a[m=2,rm=25,r=30] distance -25
		/scoreboard players set @a[m=2,rm=30,r=40] distance -30
		/scoreboard players set @a[m=2,rm=40,r=50] distance -40
		/scoreboard players set @a[m=2,rm=50,r=100] distance -50
		/scoreboard players set @a[m=2,rm=100,r=150] distance -100
		/scoreboard players set @a[m=2,rm=150,r=200] distance -150
		/scoreboard players set @a[m=2,rm=200,r=300] distance -200
		/scoreboard players set @a[m=2,rm=300,r=400] distance -300
		/scoreboard players set @a[m=2,rm=400,r=500] distance -400
		/scoreboard players set @a[m=2,rm=500] distance -500
	{"executeAs":""}

# DETECT DEATHS
!loop DetectDeaths
	{"type":"chain", "conditional":false}
	{"executeAs":"@a[m=2,score_deaths_min=1,score_timeSinceDeath_min=1]"}
		-- Tell dead players that they are out of the race
			/tellraw @p ["",{"text":"You're out of the race!\n","color":"yellow","bold":"true"},{"text":"You can now spectate until the race is complete. ","color":"none","bold":"false"}]
		-- Tell dead players that they are out of the race
			/tellraw @a ["",{"selector":"@p","color":"yellow","bold":"true"},{"text":" is out of the race!","color":"yellow","bold":"true"}]
		-- Update deaths_dummy to match deaths
			/scoreboard players set @p deaths_dummy 1
		-- Change adventure mode players to spectator when they have 1 death.
			/gamemode spectator @p
		-- Remove dead players from "Distance" objective
			/scoreboard players reset @p distance
	{"executeAs":""}
	--- Test for no more players ---
	-- Reset testforOutput
	/scoreboard players set @e[name=SYSTEM] testforOutput -1
	/stats block ~1 ~ ~ set SuccessCount @e[name=SYSTEM] testforOutput
	/testfor @a[score_deaths_dummy_min=0,score_deaths_dummy=0]
	{"executeAs":"@e[name=SYSTEM,score_testforOutput=0,score_testforOutput_min=0]"}
		-- Stop self
		!stop_loop DetectDeaths
		-- Tell everyone the game is over
		/tellraw @a ["",{"text":"Race over!\n","color":"yellow","bold":"true"},{"text":"There's nobody left alive to win...","color":"none","bold":"false"}]
		-- Turn DetectWin off
		!stop_loop DetectWin
		-- Run EndGame
		!start_event EndGame
		-- Turn off finish point
		/execute @e[tag=ActiveFinishPoint] ~ ~ ~ setblock ~ ~ ~ minecraft:air
		/scoreboard players tag @e[tag=ActiveFinishPoint] remove ActiveFinishPoint
	{"executeAs":""}	
	 -- Reset testforOutput
	/scoreboard players set @e[name=SYSTEM] testforOutput -1

# DETECT WINNER
!loop DetectWin
	{"type":"chain", "conditional":false}
	-- Execute on anyone who gets to be directly above the finish point.
	{"executeAs":"@e[tag=ActiveFinishPoint] ~ ~ ~ execute @a[m=2,dy=200]"}
		-- Turn self off
		!stop_loop DetectWin
		-- Run EndGame
		!start_event EndGame
		-- Announce winner
		/title @a title ["",{"selector":"@p","color":"green","bold":"true"},{"text":" wins! ","color":"green","bold":"true"}]
		/tellraw @a ["",{"selector":"@p","color":"green","bold":"true"},{"text":" wins the race! ","color":"green","bold":"true"}]
		-- Add 1 to the winners Total Wins
		/scoreboard players add @p total_wins 1
		-- Create Fireworks
		-- /summon FireworksRocketEntity ~ ~10 ~
		!fireworks ~ ~10 ~ Fireworks\Winner1.txt
		!fireworks ~ ~10 ~ Fireworks\Winner2.txt
		!fireworks ~ ~10 ~ Fireworks\Winner3.txt
		!fireworks ~ ~10 ~ Fireworks\Winner4.txt
		!fireworks ~ ~10 ~ Fireworks\WinnerFinal.txt
		-- Remove finish point.	
		/execute @e[tag=ActiveFinishPoint] ~ ~ ~ setblock ~ ~ ~ minecraft:air
		/scoreboard players tag @e[tag=ActiveFinishPoint] remove ActiveFinishPoint
	{"executeAs":""}
			
# END GAME
!event EndGame
	{"type":"chain", "conditional":false}
	-- Allow all players to use trigger_start again and start loop
	/scoreboard players set @a trigger_start 0
	/scoreboard players enable @a trigger_start
	!start_loop TriggerStart
	-- Show "Play Again" option	
	/tellraw @a ["",{"text":"Play again? "},{"text":"YES","color":"white","underlined":"true","clickEvent":{"action":"run_command","value":"/trigger trigger_start set 1"}}]
	-- Clear all inventories and effects
	/effect @a clear
	/clear @a
	-- Stop DetectDeaths
	!stop_loop DetectDeaths
	-- Reset all death counts
	/scoreboard players reset @a deaths
	-- Stop GameRunning 
	!stop_loop GameRunning
	-- Put the total_wins scoreboard on display
	/scoreboard objectives setdisplay sidebar total_wins
	-- Stop the power ups, call CancelPowerups
	!start_event CancelPowerups
	-- Put the time back to afternoon
	/time set 12300
	-- TEMPORARY: Put all players back to creative mode
	/gamemode creative @a
	!start_loop ShowStartPoints
	!start_loop ShowFinishPoints

