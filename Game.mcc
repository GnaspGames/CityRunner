#--- TRIGGER START ---#
<
# Allow players to trigger the start of a game

clock_trigger_start:
	/execute @a[score_trigger_start_min=1] ~ ~ ~ 
		scoreboard players set @e[name=strip_start_countdown] strips 1
	/execute @a[score_trigger_start_min=1] ~ ~ ~ 
		scoreboard players set @e[score_trigger_start_min=1] trigger_start 0
>

#--- START COUNTDOWN ---#
<
# Start a countdown that leads to the game start

# To start, use: 
/scoreboard players set @e[name=strip_start_countdown] strips 1

strip_start_countdown:
	# Set correct title times
	/title @a times 0 40 20 
	# Start countdown scoreboard
	/scoreboard players set @e[type=ArmorStand,name=CityRunner] countdown 100
	# Turn on countdown clock
	/scoreboard players set @e[name=clock_countdown] clocks 1
	# Stop players using trigger_start
	/scoreboard players reset @a trigger_start
	/scoreboard players set @e[name=clock_trigger_start] clocks 0

clock_countdown:	/

	/execute @e[type=ArmorStand,name=CityRunner,score_countdown=100,score_countdown_min=100] ~ ~ ~ 
		execute @a ~ ~ ~ 
			title @p title {text:"5",color:red,bold:false,underlined:false,italic:false,strikethrough:false,obfuscated:false}
			
	/execute @e[type=ArmorStand,name=CityRunner,score_countdown=80,score_countdown_min=80] ~ ~ ~ 
		execute @a ~ ~ ~ 
			title @p title {text:"4",color:red,bold:false,underlined:false,italic:false,strikethrough:false,obfuscated:false}
			
	/execute @e[type=ArmorStand,name=CityRunner,score_countdown=60,score_countdown_min=60] ~ ~ ~ 
		execute @a ~ ~ ~ 
			title @p title {text:"3",color:red,bold:false,underlined:false,italic:false,strikethrough:false,obfuscated:false}
			
	/execute @e[type=ArmorStand,name=CityRunner,score_countdown=40,score_countdown_min=40] ~ ~ ~ 
		execute @a ~ ~ ~ 
			title @p title {text:"2",color:red,bold:false,underlined:false,italic:false,strikethrough:false,obfuscated:false}
			
	/execute @e[type=ArmorStand,name=CityRunner,score_countdown=20,score_countdown_min=20] ~ ~ ~ 
		execute @a ~ ~ ~ 
			title @p title {text:"1",color:red,bold:false,underlined:false,italic:false,strikethrough:false,obfuscated:false}
			
	/execute @e[type=ArmorStand,name=CityRunner,score_countdown=0,score_countdown_min=0] ~ ~ ~ 
		execute @a ~ ~ ~ 
			title @p title {text:"Go!",color:green,bold:false,underlined:false,italic:false,strikethrough:false,obfuscated:false}
		
	/execute @e[type=ArmorStand,name=CityRunner,score_countdown=0,score_countdown_min=0] ~ ~ ~ 
		scoreboard players set @e[name=clock_countdown] clocks 0
		
	/execute @e[type=ArmorStand,name=CityRunner,score_countdown=0,score_countdown_min=0] ~ ~ ~ 
		scoreboard players set @e[name=strip_start] strips 1
			
	/scoreboard players remove @e[type=ArmorStand,name=CityRunner] countdown 1

>


#--- START GAME ---#
<

# To start a new game:
/scoreboard players set @e[name=strip_start] strips 1

strip_start:
	# Choose a new Start Point
	/scoreboard players set @r[type=ArmorStand,name=cr_start_point] start_points 1
	# Set up all viable spawn points around start point
	/execute @e[name=cr_start_point,score_start_points_min=1] ~ ~ ~ scoreboard players set @e[name=cr_start_spawn,r=10] start_points 1
	# Send players to a random spawn point.
	/execute @a ~ ~ ~ tp @p @r[type=ArmorStand,name=cr_start_spawn,score_start_points_min=1]
	/execute @a ~ ~ ~ spawnpoint ~ ~ ~
			
	# Reset start point scores
	/scoreboard players set @e[type=ArmorStand,name=cr_start_point] start_points 0
	/scoreboard players set @e[type=ArmorStand,name=cr_start_spawn] start_points 0

	# Give players starting effects
	/effect @a minecraft:speed 1000 3
	/give @a minecraft:ender_pearl 5
	
	# Choose a new Finish Point
	/scoreboard players set @r[type=ArmorStand,name=cr_finish_point] finish_points 1

	# Set the beacon block to turn it on
	/execute @e[name=cr_finish_point,score_finish_points_min=1] ~ ~ ~ setblock ~ ~ ~ minecraft:beacon
	
	# Turn on fill clock for win conditions
	/scoreboard players set @e[name=clock_winner] clocks 1
	
	# Put all players into adventure mode
	/gamemode adventure @a
	
	# Reset DEATHS for all players
	/scoreboard players reset @a deaths
	/scoreboard players set @a deaths_dummy 0
	
	# Start clock_deaths
	/scoreboard players set @e[name=clock_deaths] clocks 1
	
	# Start strip_start_powerups
	/scoreboard players set @e[name=strip_start_powerups] strips 1
	
	# Give everyone full health
	/effect @a minecraft:regeneration 1 20
>

#--- POWER UPS ---#
<

# TODO: Make chest content random
# TODO: Remove power ups at the end of the race
# TODO: Consider making the countdown length random, or different

random_powerup_score: /scoreboard players set @e[name=CityRunner] power_up_option 1
random_powerup_score: /scoreboard players set @e[name=CityRunner] power_up_option 2
random_powerup_score: /scoreboard players set @e[name=CityRunner] power_up_option 3
random_powerup_score: /scoreboard players set @e[name=CityRunner] power_up_option 4
random_powerup_score: /scoreboard players set @e[name=CityRunner] power_up_option 5
random_powerup_score: /scoreboard players set @e[name=CityRunner] power_up_option 6

strip_start_powerups:
	/scoreboard players set @e[name=strip_cycle_powerups] strips 1
	/scoreboard players set @e[name=clock_powerups] clocks 1
	
strip_cycle_powerups:
	/scoreboard players set @e[name=CityRunner] countdown 600
	
	/execute @e[type=ArmorStand,name=random_powerup_score] ~ ~ ~ 
		setblock ~ ~-1 ~ wool 15
		
	/execute @r[type=ArmorStand,name=random_powerup_score] ~ ~ ~ 
		setblock ~ ~-1 ~ redstone_block 0
	

clock_powerups:
	# Check countdown to trigger new power up creation
	/execute @e[name=CityRunner,score_countdown=0,score_countdown_min=0] ~ ~ ~ 
		/scoreboard players set @e[name=strip_give_powerups] strips 1
		
	# Countdown each tick
	/scoreboard players remove @e[name=CityRunner] countdown 1
	
	# Now, track cr_falling_powerup (created by strip_give_powerups) until it hits the ground, then create a chest.
	/scoreboard players set @e[name=cr_falling_powerup] power_ups 1
	
	/scoreboard players set @e[name=cr_falling_powerup] power_ups 0 {OnGround:1b} 
	
	# If score is 1, it's still in the air, show fireworks spark
	/execute @e[name=cr_falling_powerup,score_power_ups=1] ~ ~ ~ 
		particle fireworksSpark ~ ~2 ~ 0.2 0 0.2 0.1 100 force
	
	# If score is 0, it's now on the ground. Summon a chest and so on.
	/execute @e[name=CityRunner,score_power_up_option=1,score_power_up_option_min=1] ~ ~ ~ 
		execute @e[name=cr_falling_powerup,score_power_ups=0] ~ ~ ~ 
			setblock ~ ~ ~ minecraft:chest 2 replace {CustomName:- Power Up -,Items:[{Slot:13,id:ender_pearl,Count:3}]}
			
	/execute @e[name=CityRunner,score_power_up_option=2,score_power_up_option_min=2] ~ ~ ~ 
		execute @e[name=cr_falling_powerup,score_power_ups=0] ~ ~ ~  
			setblock ~ ~ ~ minecraft:chest 2 replace {CustomName:- Power Up -,Items:[{Slot:13,id:potion,Count:1,Damage: 8203,tag:{CustomPotionEffects:[{Id:1,Amplifier:10,Duration:600}],display:{Name: "Speed Boost"}}}]}
			
	/execute @e[name=CityRunner,score_power_up_option=3,score_power_up_option_min=3] ~ ~ ~ 
		execute @e[name=cr_falling_powerup,score_power_ups=0] ~ ~ ~ 
			setblock ~ ~ ~ minecraft:chest 2 replace {CustomName:- Power Up -,Items:[{Slot:13,id:potion,Count:1,Damage: 8203,tag:{CustomPotionEffects:[{Id:8,Amplifier:20,Duration:600}],display:{Name: "Jump Boost"}}}]}
			
	/execute @e[name=CityRunner,score_power_up_option=4,score_power_up_option_min=4] ~ ~ ~ 
		execute @e[name=cr_falling_powerup,score_power_ups=0] ~ ~ ~ 
			setblock ~ ~ ~ minecraft:chest 2 replace {CustomName:- Power Up -,Items:[{Slot:13,id:iron_sword,Count:1,tag:{display:{Name:"Blade Runner"}}}]}
			
	/execute @e[name=CityRunner,score_power_up_option=5,score_power_up_option_min=5] ~ ~ ~ 
		execute @e[name=cr_falling_powerup,score_power_ups=0] ~ ~ ~ 
			setblock ~ ~ ~ minecraft:chest 2 replace {CustomName:- Power Up -,Items:[{Slot:13,id:bow,Count:1},{Slot:14,id:arrow,Count:5}]}
			
	/execute @e[name=CityRunner,score_power_up_option=6,score_power_up_option_min=6] ~ ~ ~ 
		execute @e[name=cr_falling_powerup,score_power_ups=0] ~ ~ ~ 
			setblock ~ ~ ~ minecraft:chest 2 replace {CustomName:- Power Up -,Items:[{Slot:13,id:deadbush,Count:1,tag:{display:{Name:"trolololol bush"}}}]}
			
	# Summon final fireworksSpark for landing: bigger to signify impact.	
	/execute @e[name=cr_falling_powerup,score_power_ups=0] ~ ~ ~ 
		particle fireworksSpark ~ ~2 ~ 1 1 1 0.1 200 force
		
	# Play sound to replace fireworks sound
	/execute @e[name=cr_falling_powerup,score_power_ups=0] ~ ~ ~ 
		playsound random.explode @a ~ ~ ~ 1 1 1
			
	# Replace cr_falling_powerup with cr_powerup_position
	/entitydata @e[name=cr_falling_powerup,score_power_ups=0] {CustomName:"cr_powerup_position"}

strip_give_powerups:
	
	# Start by summoning markers to east/north/west/south of each player
	# EAST FACING
	/execute @a[m=2,rym=-135,ry=-45] ~ ~ ~ 
		summon ArmorStand ~10 ~ ~ {CustomName:"cr_powerup",Marker:true,Invisible:true}
		
	# NORTH FACING
	/execute @a[m=2,rym=135,ry=-135]~ ~ ~ 
		summon ArmorStand ~ ~ ~-10 {CustomName:"cr_powerup",Marker:true,Invisible:true}
	
	# WEST FACING
	/execute @a[m=2,rym=46,ry=135] ~ ~ ~ 
		summon ArmorStand ~-10 ~ ~ {CustomName:"cr_powerup",Marker:true,Invisible:true}
	
	# SOUTH FACING
	/execute @a[m=2,rym=-45,ry=45] ~ ~ ~ 
		summon ArmorStand ~ ~ ~10 {CustomName:"cr_powerup",Marker:true,Invisible:true}
		
	# Then summon Invisible ArmorStand above each marker - clock_powerups catches this.
	/execute @e[name=cr_powerup] ~ ~ ~ 
		summon ArmorStand ~ ~100 ~ {CustomName:"cr_falling_powerup",Invisible:true,Marker:true}
		
	# Kill all markers
	/kill @e[name=cr_powerup]
		
	# Run strip_start_powerups again.
	/scoreboard players set @e[name=strip_cycle_powerups] strips 1
	
	# Play a thunder sound
	/playsound ambient.weather.thunder @a ~ ~ ~ 0.2 1 0.2
	
strip_cancel_powerups:
	# Turn off clock and reset countdown
	/scoreboard players set @e[name=clock_powerups] clocks 0
	# Kill any power ups that are falling
	/kill @e[name=cr_falling_powerup]
	# Disable counter. Set to -1 so that last run of clock_powerups doesn't trigger a new powerup.
	/scoreboard players set @e[name=CityRunner] countdown -1
	# Remove left over powerups
	/execute @e[name=cr_powerup_position] ~ ~ ~ 
		setblock ~ ~ ~ minecraft:air 0
	# Remove cr_powerup_position
	/kill @e[name=cr_powerup_position]
	
>

#--- CHECK FOR DEAHTS ---#
<

clock_deaths:
	# Tell dead players that they are out of the race
	/execute @a[m=2,score_deaths_min=1,score_timeSinceDeath_min=1] ~ ~ ~ 
		tellraw @a[m=2,score_deaths_min=1,score_timeSinceDeath_min=1] ["",{"text":"You're out of the race!\n","color":"yellow","bold":"true"},{"text":"You can now spectate until the race is complete. ","color":"none","bold":"false"}]
		
	# Tell dead players that they are out of the race
	/execute @a[m=2,score_deaths_min=1,score_timeSinceDeath_min=1] ~ ~ ~ 
		execute @p ~ ~ ~ 
			tellraw @a ["",{"selector":"@p","color":"yellow","bold":"true"},{"text":" is out of the race!","color":"yellow","bold":"true"}]
			
	# Update deaths_dummy to match deaths
	/execute @a[m=2,score_deaths_min=1,score_timeSinceDeath_min=1] ~ ~ ~ 
		scoreboard players set @p deaths_dummy 1

	# Change adventure mode players to spectator when they have 1 death.
	/execute @a[m=2,score_deaths_min=1,score_timeSinceDeath_min=1] ~ ~ ~ 
		gamemode spectator @p
		
	#--- Test for no more players ---#
	
	# Reset testforOutput
	/scoreboard players set @e[name=CityRunner] testforOutput -1
	
	/stats block ~ ~2 ~ set SuccessCount @e[name=CityRunner] testforOutput
	/testfor @a[score_deaths_dummy_min=0,score_deaths_dummy=0]
	
	# Stop self
	/execute @e[name=CityRunner,score_testforOutput=0,score_testforOutput_min=0] ~ ~ ~ 
		scoreboard players set @e[name=clock_deaths] clocks 0
	
	# Tell everyone the game is over
	/execute @e[name=CityRunner,score_testforOutput=0,score_testforOutput_min=0] ~ ~ ~ 
		tellraw @a ["",{"text":"Race over!\n","color":"yellow","bold":"true"},{"text":"There's nobody left alive to win...","color":"none","bold":"false"}]
		
	# Turn clock_winner off
	/execute @e[name=CityRunner,score_testforOutput=0,score_testforOutput_min=0] ~ ~ ~ 
		scoreboard players set @e[name=clock_winner] clocks 0
		
	# Run strip_win
	/execute @e[name=CityRunner,score_testforOutput=0,score_testforOutput_min=0] ~ ~ ~ 
		scoreboard players set @e[name=strip_win] strips 1
		
	# Turn off finish point
	/execute @e[name=CityRunner,score_testforOutput=0,score_testforOutput_min=0] ~ ~ ~ 
		scoreboard players set @e[type=ArmorStand,name=cr_finish_point] finish_points 0
		
	# Reset testforOutput
	/scoreboard players set @e[name=CityRunner] testforOutput -1
>

#--- CHECK FOR WINNER ---#
<

clock_winner:
	# Execute on anyone who gets to be directly above the finish point.
		
	# Turn self off
	/execute @e[name=cr_finish_point,score_finish_points_min=1] ~ ~ ~ execute @a[m=2,dy=200] ~ ~ ~ 
		scoreboard players set @e[name=clock_winner] clocks 0
		
	# Run strip_win
	/execute @e[name=cr_finish_point,score_finish_points_min=1] ~ ~ ~ execute @a[m=2,dy=200] ~ ~ ~ 
		scoreboard players set @e[name=strip_win] strips 1
		
	# Announce winner
	/execute @e[name=cr_finish_point,score_finish_points_min=1] ~ ~ ~ execute @a[m=2,dy=200] ~ ~ ~ 
		/tellraw @a ["",{"selector":"@p","color":"green","bold":"true"},{"text":" wins the race! ","color":"green","bold":"true"}]
		
	# Add 1 to the winners Total Wins
	/execute @e[name=cr_finish_point,score_finish_points_min=1] ~ ~ ~ execute @a[m=2,dy=200] ~ ~ ~ 
		scoreboard players add @p total_wins 1
		
	# Create Fireworks
	/execute @e[name=cr_finish_point,score_finish_points_min=1] ~ ~ ~ execute @a[m=2,dy=200] ~ ~ ~ 
		summon FireworksRocketEntity ~ ~10 ~ 
		{
			LifeTime:20, FireworksItem:
			{
				id:401, Count:1, tag:
				{
					Fireworks:
					{
						Explosions:
						[
							{ Flicker:1, Trail:1, Type:1, Colors:[0], FadeColors:[0] },
							{ Flicker:1, Trail:1, Type:2, Colors:[16777215], FadeColors:[16777215] },
							{ Flicker:1, Trail:1, Type:4, Colors:[15000], FadeColors:[15000] }
						]
					}
				}
			}
		}
		
	# Remove finish point.	
	/execute @e[name=cr_finish_point,score_finish_points_min=1] ~ ~ ~ execute @a[m=2,dy=200] ~ ~ ~ 
		scoreboard players set @e[type=ArmorStand,name=cr_finish_point] finish_points 0
			
strip_win:

	# Turn off all finish point beacons (should only be 1 really)
	/execute @e[name=cr_finish_point] ~ ~ ~ setblock ~ ~ ~ minecraft:air

	# Allow all players to use trigger_start again
	/scoreboard players enable @a trigger_start
	/scoreboard players set @e[name=clock_trigger_start] clocks 1

	# Show "Play Again" option	
	/tellraw @a ["",{"text":"Play again? "},{"text":"YES","color":"white","underlined":"true","clickEvent":{"action":"run_command","value":"/trigger trigger_start set 1"}}]

	# Clear all inventories and effects
	/effect @a clear
	/clear @a

	# TEMPORARY: Put all players back to creative mode
	/gamemode creative @a
	
	# Stop clock_deaths
	/scoreboard players set @e[name=clock_deaths] clocks 0
	
	# Reset all death counts
	/scoreboard players reset @a deaths
	
	# Stop the power ups, call strip_cancel_powerups
	/scoreboard players set @e[name=CityRunner] countdown 400
	
>